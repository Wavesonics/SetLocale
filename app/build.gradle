apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.darkrockstudios.apps.setlocale"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 3
        versionName "1.2"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release {

        }
    }
    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            signingConfig = android.signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        println 'gradle.properties not well formed'
        if (!project.hasProperty('storeFile')) println 'storeFile not defined'
        if (!project.hasProperty('storePassword')) println 'storePassword not defined'
        if (!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if (!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // You must install or update the Support Repository through the SDK manager to use this dependency.
    compile 'com.android.support:support-annotations:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:support-v13:21.0.0'

    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.colintmiller:simplenosql:0.2.3'
}
